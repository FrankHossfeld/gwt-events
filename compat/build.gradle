plugins {
  id "java-library"
  id "maven"
}
apply from: "$rootDir/gradle/gwtTest.gradle"

dependencies {
  api project(":")
  api("com.google.gwt:gwt-user:2.8.2")

  testImplementation "junit:junit:4.12"
}

task generateTestSources(type: Copy) {
  into "$buildDir/generated-sources/java-templates/test/"
  from('src/test/java-templates/') {
    rename { it.replace('__variant__', 'Bindery') }
    filter {
      it.replace('__variant__', 'Bindery')
    }
  }
  from('src/test/java-templates/') {
    rename { it.replace('__variant__', 'Gwt') }
    filter {
      it.replace('__variant__', 'Gwt')
        .replace('FooEvent', 'FooGwtEvent')
        .replace('BarEvent', 'BarGwtEvent')
    }
  }
}
sourceSets.test.java.srcDirs files(generateTestSources.destinationDir).builtBy(generateTestSources)
licenseTest.source fileTree('src/test/java-templates/')
licenseFormatTest.source fileTree('src/test/java-templates/')

generateGwtTestSources.ext.gwtTestModule = "org.gwtproject.event.compat.EventCompat"
