plugins {
  id "java-library"
}
apply plugin: "net.ltgt.errorprone"
apply from: "$rootDir/gradle/gwtTest.gradle"

repositories {
  mavenCentral()
}

dependencies {
  errorprone "com.google.errorprone:error_prone_core:2.1.2"

  api project(":")
  api("com.google.gwt:gwt-user:2.8.2")

  testImplementation "junit:junit:4.12"
}

sourceCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
  options.compilerArgs << "-Werror" << "-Xlint:all"
}

jar {
  from sourceSets.main.allJava
}

task generateTestSources(type: Copy) {
  into "$buildDir/generated-sources/java-templates/test/"
  from('src/test/java-templates/') {
    rename { it.replace('__variant__', 'Bindery') }
    filter {
      it.replace('__variant__', 'Bindery')
    }
  }
  from('src/test/java-templates/') {
    rename { it.replace('__variant__', 'Gwt') }
    filter {
      it.replace('__variant__', 'Gwt')
        .replace('FooEvent', 'FooGwtEvent')
        .replace('BarEvent', 'BarGwtEvent')
    }
  }
}
sourceSets.test.java.srcDirs files(generateTestSources.destinationDir).builtBy(generateTestSources)

test {
  include "**/*Suite.class"
}
generateGwtTestSources.ext.gwtTestModule = "org.gwtproject.event.compat.EventCompat"

javadoc {
  options.addBooleanOption("Xdoclint:all,-missing", true)
}
