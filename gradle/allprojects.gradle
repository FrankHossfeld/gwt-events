allprojects {
  group = "org.gwtproject.event"
  version = "HEAD-SNAPSHOT"

  repositories {
    mavenCentral()
  }

  plugins.withId('java') {
    sourceCompatibility = JavaVersion.VERSION_1_8

    jar {
      from sourceSets.main.allJava
    }

    test {
        include "**/*Suite.class"
    }

    javadoc {
        options.addBooleanOption("Xdoclint:all,-missing", true)
    }

    apply plugin: 'net.ltgt.errorprone'
  }
  plugins.withId('net.ltgt.errorprone') {
    dependencies {
      errorprone 'com.google.errorprone:error_prone_core:2.2.0'
    }
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Werror' << '-Xlint:all'
    if (JavaVersion.current().isJava9Compatible()) {
      options.compilerArgs << '--release' << '8'
    }
  }

  apply plugin: 'com.github.sherter.google-java-format'
  googleJavaFormat {
    toolVersion = "1.5"
  }

  apply plugin: 'com.github.hierynomus.license'
  license {
    header = rootProject.file('LICENSE.header')
    encoding = 'UTF-8'
    skipExistingHeaders = true
    mapping java: 'SLASHSTAR_STYLE'

    ext.year = java.time.Year.now()
    ext.name = 'The GWT Project Authors'
  }
}
